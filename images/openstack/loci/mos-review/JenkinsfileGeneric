import groovy.json.JsonOutput

EVENT_TYPE = env.EVENT_TYPE ? env.EVENT_TYPE : 'manual'

BUILD_TYPE_MAP = [
    'mos-nova': ['nova', 'nova-1804'],
    'mos-neutron': ['neutron', 'neutron-sriov'],
]

def getProjName(String project) {
    project.split('/')[-1]
}

def getRefParamName(String project) {
    getProjName(project).toUpperCase().replace('-', '_') + "_REF"
}

reqRefParam = getRefParamName(env.REQ_PROJECT_NAME)
reqRef = env."${reqRefParam}"

if (REQUIREMENTS_LOCI_IMAGE && (DEPENDENCY_LIST || reqRef)) {
    error('Can not specify REQUIREMENTS_LOCI_IMAGE with DEPENDENCY_LIST ' +
          'or ${reqRefParam}')
}

stage("${REQ_PROJECT_NAME} image build") {
    if (DEPENDENCY_LIST || reqRef) {
        reqRef = reqRef ? reqRef : BRANCH
        parameters = [
            stringParam(name: 'PROJECT_NAME', value: REQ_PROJECT_NAME),
            stringParam(name: 'DEPENDENCY_LIST', value: DEPENDENCY_LIST),
            stringParam(name: 'EVENT_TYPE', value: EVENT_TYPE),
            stringParam(name: 'PROJECT_REF', value: reqRef),
        ]
        print "Building image for ${REQ_PROJECT_NAME} with ${parameters}"
        job = build(
            job: "${JOB_BASE}/GenericImageBuildPipeline",
            wait: true,
            propagate: true,
            parameters: parameters
        )
        REQUIREMENTS_LOCI_IMAGE = job.getBuildVariables()["IMAGE_SHA"]
    } else {
        print ("Skipping build for ${REQ_PROJECT_NAME} as there " +
               'are no related changes.')
    }
}

PROJECT_LIST = env.PROJECT_LIST.split()
results = []
runningSet = [:]
PROJECT_LIST.each { projectName ->
    if (projectName == REQ_PROJECT_NAME) { return }
    // Don't build component image with both default refspec and
    // default requirement image
    if (!(REQUIREMENTS_LOCI_IMAGE || env."${getRefParamName(projectName)}")) {
        print ("Skipping build for ${projectName} as there are no " +
               "related changes.")
        return
    }
    buildTypes = BUILD_TYPE_MAP.get(projectName, [projectName.split('-')[-1]])
    buildTypes.each { buildType ->
        runningSet[buildType] = {
            stage("${buildType} image build") {
                projectRef = env."${getRefParamName(projectName)}"
                projectRef = projectRef ? projectRef : BRANCH
                parameters = [
                    stringParam(name: 'PROJECT_NAME', value: projectName),
                    stringParam(name: 'PROJECT_REF', value: projectRef),
                    stringParam(name: 'BUILD_TYPE', value: buildType),
                    stringParam(name: 'REQUIREMENTS_LOCI_IMAGE',
                                value: REQUIREMENTS_LOCI_IMAGE),
                    stringParam(name: 'EVENT_TYPE', value: EVENT_TYPE),
                ]
                print "Building image for ${buildType} with ${parameters}"
                job = build(
                    job: "${JOB_BASE}/GenericImageBuildPipeline",
                    parameters: parameters,
                    propagate: true
                )
                results.add(job)
            }
        }
    }
}

parallel runningSet

overrideImages = [:]
results.each {
    buildVars = it.getBuildVariables()
    overrideImages[buildVars["LOCI_IMAGE_VAR"]] = buildVars["IMAGE_SHA"]
}
overrideImagesJSON = JsonOutput.prettyPrint(JsonOutput.toJson(overrideImages))
parameters = [
    stringParam(name: "OVERRIDE_IMAGES", value: overrideImagesJSON)
]

// publish all newly created images
node(LOCI_BUILD_SLAVE_LABEL) {
    images = ""
    overrideImages.each { k, v -> images += "${k}=${v}\n"}
    if (REQUIREMENTS_LOCI_IMAGE) {
        images += "REQUIREMENTS_LOCI=${REQUIREMENTS_LOCI_IMAGE}"
    }
    sh "mkdir -p ${WORKSPACE}/artifacts"
    sh "echo '${images}' > ${WORKSPACE}/artifacts/images.txt"
    archiveArtifacts 'artifacts/*'
}

stage("Test Deployment") {
    job = build(
        job: "${JOB_BASE}/TestDeploymentPipeline",
        parameters: parameters,
        wait: true,
        propagate: true
    )
}
